apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.shakeup.setofthree"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "0.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // Add support library use for vector drawables
        vectorDrawables.useSupportLibrary = true

        // Variable to provide different Content Provider Authorities to different builds
        manifestPlaceholders = [score_provider_authority: ""]
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
            manifestPlaceholders = [score_provider_authority: "com.shakeup.setofthree.provider"]
        }
        debug {
            applicationIdSuffix '.debug'
            debuggable true
            manifestPlaceholders = [score_provider_authority: "com.shakeup.setofthree.debug.provider"]
        }
    }
    productFlavors {
    }
}

repositories {
    maven {
        url "https://maven.google.com"
    }
}

dependencies {
    // Declare dependencies for project modules
    compile(project(':BaseGameUtils'))
    compile project(':setgamelibrary')

    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:26.0.0'
    compile 'com.android.support:support-v4:26.0.0'
    compile 'com.android.support:cardview-v7:26.0.0'
    compile 'com.android.support:gridlayout-v7:26.0.0'
    compile 'com.android.support:design:26.0.0'
    compile 'com.android.support:recyclerview-v7:26.0.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.1'
    compile 'javax.annotation:javax.annotation-api:1.2'
    compile 'com.google.guava:guava:20.0'

    /*
    * 3rd Party Libraries
    */
    // Library to display rotated views
    compile 'rongi.rotate-layout:rotate-layout:3.0.0'
    // Library to display buttons with progress bars
    compile 'com.github.dmytrodanylyk.android-process-button:library:1.0.0'
    // Content Provider Generation library
    annotationProcessor 'net.simonvt.schematic:schematic-compiler:0.7.0'
    compile 'net.simonvt.schematic:schematic:0.7.0'
    // Parceler library generate parcelable implementations
    compile 'org.parceler:parceler-api:1.1.6'
    annotationProcessor 'org.parceler:parceler:1.1.6'

    // Dependencies for local unit tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.powermock:powermock-module-junit4:1.6.2'
    testCompile 'org.powermock:powermock-api-mockito:1.6.2'

    // Android Testing Support Library's runner and rules
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    // Espresso UI Testing dependencies.
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'recyclerview-v7'
    }
    // Re-add support dependencies to ensure androidTestCompile uses the latest versions
    androidTestCompile 'com.android.support:appcompat-v7:26.0.0'
    androidTestCompile 'com.android.support:support-v4:26.0.0'
    androidTestCompile 'com.android.support:support-annotations:26.0.0'
}
